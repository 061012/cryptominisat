AC_INIT(cryptominisat, 2.9.0)

dnl use libtool
AC_PROG_LIBTOOL

dnl we are C++
AC_LANG_CPLUSPLUS
if test -n "${CXXFLAGS}"; then
  user_set_cxxflags=yes
fi
AC_PROG_CXX
if test X$user_set_cxxflags != Xyes; then
  CXXFLAGS="-g -O2"
fi
AC_LANG(C++)

AC_MSG_CHECKING([for gcc bug number 47365])
AC_TRY_RUN([
    struct A
    {
    int i;
    };

    struct B
    {
    struct A a[2];
    };

    int i = 1;
    struct B b = { 0, 3 };

    static int
    test()
    {
    if (b.a[0].i != i)
        {
        int t = b.a[0].i;
        /* passes with c.a[0].i = c.a[1].i */
        b.a[0] = b.a[1];
        b.a[1].i = t;
        }

    if (b.a[1].i == i)
        return 1;

    if (b.a[0].i == 0)
        return 0;
    }

    int
    main ()
    {
    return test();
    }
],[FPRE_BUG=no], [FPRE_BUG=yes])

if test "x$FPRE_BUG" = "xyes" ; then
    AC_MSG_RESULT([Your gcc is broken. Disabling -ftree-pre, but this may slow down CryptoMS])
    CXXFLAGS="$CXXFLAGS -fno-tree-pre"
else
    AC_MSG_RESULT([Your gcc version doesn't _seem_ broken])
fi

dnl AC_CONFIG_AUX_DIR([m4-extra])
AC_CONFIG_MACRO_DIR([m4])
AM_INIT_AUTOMAKE

AC_CONFIG_HEADERS(config.h)
AC_CHECK_HEADERS(iostream)

# checks for compiler characteristics
AC_CHECK_SIZEOF([int])
AC_CHECK_SIZEOF([long])
AC_CHECK_SIZEOF([size_t])

# checks for library functions
AC_FUNC_MALLOC
AC_FUNC_MEMCMP

#statistics generation
#AH_TEMPLATE([STATS_NEEDED],[Enable statistics generation code])
#AC_DEFINE([STATS_NEEDED])

#gauss generation needed
AH_TEMPLATE([USE_GAUSS],[Enable gaussian elimination code])
AC_DEFINE([USE_GAUSS])

#search for openmp
AC_OPENMP

AC_OUTPUT(Makefile Solver/Makefile)
